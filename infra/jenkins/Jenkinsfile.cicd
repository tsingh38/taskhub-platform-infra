pipeline {
  agent any

  parameters {
    booleanParam(
      name: 'RELEASE',
      defaultValue: false,
      description: 'Manual PROD intent. If true, pipeline deploys to PROD using RELEASE_TAG.'
    )
    string(
      name: 'RELEASE_TAG',
      defaultValue: '',
      description: 'Required when RELEASE=true. Example: 0.1.0 (must exist as docker tag)'
    )
  }

  environment {
    REGISTRY = "tsingh38"
    IMAGE_NAME = "taskhub"
    DOCKERHUB_CREDENTIALS = "dockerhub-tsingh38-taskhub"
    KUBECONFIG = "/var/lib/jenkins/.kube/config"

    TF_STATE_MONITORING = "/var/lib/jenkins/terraform-state/taskhub-monitoring/terraform.tfstate"
    TF_STATE_DEV        = "/var/lib/jenkins/terraform-state/taskhub-dev/terraform.tfstate"
    TF_STATE_PROD       = "/var/lib/jenkins/terraform-state/taskhub-prod/terraform.tfstate"

    TRIVY_CACHE_DIR = "/var/lib/jenkins/.cache/trivy"
  }

  stages {

    stage('Validate Release Params') {
      steps {
        script {
          if (params.RELEASE) {
            if (!params.RELEASE_TAG?.trim()) {
              error("RELEASE=true requires RELEASE_TAG (e.g. 0.1.0).")
            }
          }
        }
      }
    }

    stage('Checkout') {
      steps { checkout scm }
    }

    stage('Resolve Version') {
      steps {
        dir('services/task-service') {
          script {
            def version = sh(
              script: "./gradlew properties -q | grep '^version:' | awk '{print \$2}'",
              returnStdout: true
            ).trim()
            env.APP_VERSION = version
          }
        }
      }
    }

    stage('Compute Image Tag') {
      steps {
        script {
          def imageTag = (params.RELEASE && params.RELEASE_TAG?.trim())
            ? params.RELEASE_TAG.trim()
            : "${env.APP_VERSION}-${env.BUILD_NUMBER}"

          env.IMAGE_TAG = imageTag
          env.IMAGE = "${env.REGISTRY}/${env.IMAGE_NAME}:${imageTag}"

          echo "IMAGE_TAG=${env.IMAGE_TAG}"
          echo "IMAGE=${env.IMAGE}"
        }
      }
    }

    stage('Build & Test') {
      steps {
        dir('services/task-service') {
          sh '''
            set -eu
            chmod +x gradlew
            ./gradlew clean test
          '''
        }
      }
      post {
        always {
          junit allowEmptyResults: true, testResults: 'services/task-service/build/test-results/test/*.xml'
          archiveArtifacts artifacts: 'services/task-service/build/reports/tests/test/**', allowEmptyArchive: true
        }
      }
    }

    stage('Docker Build & Push') {
      steps {
        dir('services/task-service') {
          withCredentials([usernamePassword(
            credentialsId: DOCKERHUB_CREDENTIALS,
            usernameVariable: 'DOCKER_USER',
            passwordVariable: 'DOCKER_PASS'
          )]) {
            sh '''
              set -eu
              echo "$DOCKER_PASS" | docker login -u "$DOCKER_USER" --password-stdin
              docker build -t "$IMAGE" .
              docker push "$IMAGE"
              echo "Built and pushed: $IMAGE"
            '''
          }
        }
      }
    }

    stage('Trivy Scan (HIGH/CRITICAL gate)') {
      steps {
        dir('services/task-service') {
          withCredentials([usernamePassword(
            credentialsId: DOCKERHUB_CREDENTIALS,
            usernameVariable: 'DOCKER_USER',
            passwordVariable: 'DOCKER_PASS'
          )]) {
            sh '''
              set -eu
              mkdir -p "$TRIVY_CACHE_DIR"

              docker run --rm \
                -v "$TRIVY_CACHE_DIR:/root/.cache/" \
                -v "$WORKSPACE:/work" \
                aquasec/trivy:latest \
                image \
                --timeout 5m \
                --no-progress \
                --severity HIGH,CRITICAL \
                --exit-code 1 \
                --format json \
                -o /work/trivy-report.json \
                --username "$DOCKER_USER" \
                --password "$DOCKER_PASS" \
                "$IMAGE"
            '''
          }
        }
      }
      post {
        always {
          archiveArtifacts artifacts: 'trivy-report.json', fingerprint: true, onlyIfSuccessful: false
        }
      }
    }

    stage('Deploy MONITORING') {
      when {
        expression {
          return (env.GIT_BRANCH == 'origin/develop' || env.GIT_BRANCH == 'develop' || env.BRANCH_NAME == 'develop')
        }
      }
      steps {
        dir('infra/terraform/monitoring') {
          withCredentials([string(credentialsId: 'slack-webhook-url', variable: 'SLACK_WEBHOOK')]) {
            sh '''
              set -eu
              test -f "$KUBECONFIG"
              mkdir -p "$(dirname "$TF_STATE_MONITORING")"

              export TF_VAR_kubeconfig_path="$KUBECONFIG"
              export TF_VAR_slack_webhook_url="$SLACK_WEBHOOK"

              terraform init -reconfigure -input=false -backend-config="path=$TF_STATE_MONITORING"
              terraform apply -auto-approve -input=false
            '''
          }
        }
      }
    }

    stage('Deploy DEV') {
      when {
        expression {
          return (env.GIT_BRANCH == 'origin/develop' || env.GIT_BRANCH == 'develop' || env.BRANCH_NAME == 'develop')
        }
      }
      steps {
        dir('infra/terraform/dev') {
          withCredentials([
            string(credentialsId: 'db-user', variable: 'DB_USER_DEV'),
            string(credentialsId: 'db-password', variable: 'DB_PASSWORD_DEV')
          ]) {
            sh '''
              set -eu
              test -f "$KUBECONFIG"
              mkdir -p "$(dirname "$TF_STATE_DEV")"

              export TF_VAR_kubeconfig_path="$KUBECONFIG"
              export TF_VAR_app_version="$IMAGE_TAG"
              export TF_VAR_db_user_dev="$DB_USER_DEV"
              export TF_VAR_db_password_dev="$DB_PASSWORD_DEV"

              terraform init -reconfigure -input=false -backend-config="path=$TF_STATE_DEV"
              terraform apply -auto-approve -input=false
            '''
          }
        }
      }
    }

    stage('Deploy PROD') {
      when {
        expression {
          return (params.RELEASE == true) &&
                 (params.RELEASE_TAG?.trim()) &&
                 (env.GIT_BRANCH == 'origin/main' || env.GIT_BRANCH == 'main' || env.BRANCH_NAME == 'main')
        }
      }
      steps {
        dir('infra/terraform/prod') {
          withCredentials([
            string(credentialsId: 'db-user-prod', variable: 'DB_USER_PROD'),
            string(credentialsId: 'db-password-prod', variable: 'DB_PASSWORD_PROD')
          ]) {
            sh '''
              set -eu
              test -f "$KUBECONFIG"
              mkdir -p "$(dirname "$TF_STATE_PROD")"

              export TF_VAR_kubeconfig_path="$KUBECONFIG"
              export TF_VAR_app_version="$IMAGE_TAG"
              export TF_VAR_db_user_prod="$DB_USER_PROD"
              export TF_VAR_db_password_prod="$DB_PASSWORD_PROD"

              terraform init -reconfigure -input=false -backend-config="path=$TF_STATE_PROD"
              terraform apply -auto-approve -input=false
            '''
          }
        }
      }
    }
  }

  post {
    success { echo "Pipeline Success" }
    failure { echo "Pipeline Failed" }
  }
}