pipeline {
  agent any

  options {
    disableConcurrentBuilds()
    skipDefaultCheckout(true)
    timestamps()
  }

  parameters {
    string(
      name: 'RELEASE_TAG',
      defaultValue: '',
      description: 'Docker tag to deploy to PROD (must exist). Example: 0.1.0'
    )
    booleanParam(
      name: 'CONFIRM_PROD',
      defaultValue: false,
      description: 'Safety switch: must be TRUE to deploy to PROD'
    )
  }

  environment {
    KUBECONFIG = "/var/lib/jenkins/.kube/config"

    NAMESPACE  = "prod"
    RELEASE    = "task-service"
    IMAGE_REPO = "tsingh38/taskhub"

    CHART_DIR   = "infra/helm/charts/task-service"
    VALUES_PROD = "infra/helm/values/task-service/values-prod.yaml"
  }

  stages {

    stage('Checkout') {
      steps { checkout scm }
    }

    stage('Validate') {
      steps {
        script {
          if (!params.CONFIRM_PROD) {
            error("Blocked: set CONFIRM_PROD=true to deploy PROD.")
          }

          def tag = (params.RELEASE_TAG ?: '').trim()
          if (!tag) {
            error("RELEASE_TAG is required for PROD deploy.")
          }
          if (tag == "KEEP_CURRENT") {
            error("KEEP_CURRENT is NOT allowed for PROD. Provide a real tag (e.g. 0.1.0).")
          }

          // store as env var for later stages (safe)
          env.EFFECTIVE_TAG = tag

          echo "Deploying PROD: release=${env.RELEASE}, ns=${env.NAMESPACE}, tag=${env.EFFECTIVE_TAG}"
        }

        sh '''
          set -eu
          test -f "$KUBECONFIG"
          test -d "$CHART_DIR"
          test -f "$WORKSPACE/$VALUES_PROD"
        '''
      }
    }

    stage('Helm Upgrade PROD (task-service only)') {
      steps {
        dir("${env.CHART_DIR}") {
          script {
            // IMPORTANT: inject env.EFFECTIVE_TAG via Groovy interpolation to avoid empty shell vars
            sh """
              set -eu
              export KUBECONFIG="${env.KUBECONFIG}"

              helm upgrade --install "${env.RELEASE}" . \\
                -n "${env.NAMESPACE}" --create-namespace \\
                -f "${env.WORKSPACE}/${env.VALUES_PROD}" \\
                --set image.repository="${env.IMAGE_REPO}" \\
                --set image.tag="${env.EFFECTIVE_TAG}" \\
                --wait --timeout 7m --atomic
            """
          }
        }
      }
    }

    stage('Post-deploy checks') {
      steps {
        sh '''
          set -eu
          export KUBECONFIG="$KUBECONFIG"

          echo "PROD Pods:"
          kubectl -n prod get pods -o wide || true
          echo ""
          echo "PROD Deployment:"
          kubectl -n prod get deploy task-service -o wide || true
          echo ""
          echo "PROD Helm Status:"
          helm -n prod status task-service | sed -n '1,80p' || true
        '''
      }
    }
  }

  post {
    success { echo "PROD Deploy Success (RELEASE_TAG=${params.RELEASE_TAG})" }
    failure { echo "PROD Deploy Failed (RELEASE_TAG=${params.RELEASE_TAG})" }
  }
}